// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
      Register(in=registerIn, load=true, out=regIncOut, out=registerOut, out=out);
      Inc16(in=regIncOut, out=incOut);
      Mux16(a=registerOut, b=incOut, sel=inc, out=incCheck);
      Mux16(a=incCheck, b=in, sel=load, out=loadCheck);
      Mux16(a=loadCheck, b=false, sel=reset, out=registerIn);
}
